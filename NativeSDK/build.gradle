apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':CitrusLibrary')
    compile project(':library')
}

task copyright {
    def year = new GregorianCalendar().get(Calendar.YEAR)
    def copyright = """/*
   Copyright ${year} Citrus Payment Solutions Pvt. Ltd.

   Licensed under the Apache License, Version 2.0 (the \"License\");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an \"AS IS\" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
*/

"""

    sourceSets.all {
        allJava.srcDirs.findAll {
            it.exists() && it.directory
        }.each {
            it.eachFileRecurse {
                if (it.file && it.name.endsWith('java')) {
                    def src = (it.text =~ /(?s)^\s*\/\*.*\*\/\s*/).replaceFirst(copyright)
                    it.write(src)
                }
            }
        }
    }
}